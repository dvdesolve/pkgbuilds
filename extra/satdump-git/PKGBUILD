# Maintainer: Viktor Drobot (aka dviktor) linux776 [at] gmail [dot] com
# Contributor: Andrew O'Neill <andrew at haunted dot sh>

pkgname=satdump-git
pkgver=r5137.460b9256
pkgrel=1
pkgdesc="A generic satellite processing software"
url="https://github.com/SatDump/SatDump"
license=(GPL3)
arch=(x86_64)
depends=(airspy fftw glfw hackrf jemalloc libad9361 libpng libtiff libvolk nng ocl-icd portaudio rtl-sdr zstd)
makedepends=(git boost cmake opencl-headers)
optdepends=(limesuite bladerf)
provides=(satdump)
conflicts=(satdump)
source=("${pkgname}::git+https://github.com/SatDump/SatDump.git")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}"

  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${pkgname}"

  mkdir build
  cd build

  cmake -DCMAKE_BUILD_TYPE=Release ..
  make
}

package() {
  cd "${srcdir}/${pkgname}"

  cd build
  make DESTDIR="${pkgdir}" install
}
