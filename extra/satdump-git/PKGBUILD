# Maintainer: Viktor Drobot (aka dviktor) linux776 [at] gmail [dot] com
# Contributor: Andrew O'Neill <andrew at haunted dot sh>

pkgname=satdump-git
pkgver=r5749.c13af838e
pkgrel=1
pkgdesc="A generic satellite processing software"
url="https://github.com/SatDump/SatDump"
license=(GPL-3.0-only)
arch=(x86_64)
depends=(airspy curl fftw gcc-libs glfw glibc hackrf jemalloc libad9361 libglvnd libiio libpng libtiff libusb libvolk nng ocl-icd portaudio rtl-sdr zenity zlib zstd)
makedepends=(boost cmake git opencl-headers)
optdepends=(bladerf limesuite)
conflicts=(satdump)
provides=(satdump)
source=("${pkgname}::git+https://github.com/SatDump/SatDump.git")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}"

  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${pkgname}"

  mkdir build
  cd build

  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr -Wno-dev ..
  make
}

package() {
  cd "${srcdir}/${pkgname}"

  cd build
  make DESTDIR="${pkgdir}" install
}
