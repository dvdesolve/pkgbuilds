# Maintainer: Viktor Drobot (aka dviktor) linux776 [at] gmail [dot] com
# Contributor: Fabio 'Lolix' Loli <fabio.loli@disroot.org>
# Contributor: Zen Wen <zen.8841@gmail.com>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=qt5-webkit
_pkgver=5.212.0-alpha4
_basever=5.15.3
pkgver=${_pkgver/-/}
pkgrel=27
pkgdesc='Classes for a WebKit2 based implementation and a new QML API'
url='https://github.com/qtwebkit/qtwebkit'
license=(LGPL-2.1-only)
arch=(x86_64)
depends=(gcc-libs glib2 glibc gst-plugins-base gst-plugins-base-libs gstreamer hyphen libicui18n.so libicuuc.so libjpeg-turbo libpng libwebp libx11 libxcomposite libxml2 qt5-base qt5-declarative qt5-location qt5-sensors qt5-webchannel sqlite woff2 zlib)
makedepends=(cmake gperf ruby ruby-erb python qt5-doc qt5-tools)
optdepends=('gst-plugins-good: Webm codec support')
source=("https://github.com/qtwebkit/qtwebkit/releases/download/qtwebkit-${_pkgver}/qtwebkit-${_pkgver}.tar.xz"
        "https://src.fedoraproject.org/rpms/qt5-qtwebkit/raw/rawhide/f/qtwebkit-cstdint.patch"
        "https://src.fedoraproject.org/rpms/qt5-qtwebkit/raw/rawhide/f/qtwebkit-fix-build-gcc14.patch"
        "https://src.fedoraproject.org/rpms/qt5-qtwebkit/raw/rawhide/f/webkit-offlineasm-warnings-ruby27.patch"
        "qt5-webkit-icu75.patch::https://github.com/qtwebkit/qtwebkit/commit/756e1c8f23dc2720471298281c421c0076d02df8.patch"
         icu68.patch
         glib-2.68.patch
         qt5-webkit-python-3.9.patch
         qt5-webkit-bison-3.7.patch
         qt5-webkit-icu76.patch)
sha256sums=('9ca126da9273664dd23a3ccd0c9bebceb7bb534bddd743db31caf6a5a6d4a9e6'
            '4c71c958eae45cae65c9f002024eb1369d06029b668e595158138ff7971e64f1'
            'eea38db22078700887bf22b6a49bb628fd8444cdb2e506770c993df883d0e8fb'
            '8768433ff3f641b506962ed22cc596eaf57bf21b6d3402e0e73ad8c2afeaa502'
            'b4d1ba1e99e28fd8cb1ec82252373c870ede683869a9cd43c8e465fe09531bcb'
            '0b40ed924f03ff6081af610bb0ee01560b7bd1fb68f8af02053304a01d4ccdf0'
            '4969dd03e482155e2490b50307dada81dda7bbc9e5398e3a53c20bc474f7c04e'
            '6e0cee08e4fa57b04752e80817f33562f48aa42608a3a620930b6040259b4932'
            '34f37b53ee0bc31c63ce85ebd1ae95543a8ba28483e387b20efd50574bd813be'
            '2e360197a23c6274d7190113381ad497b2bba60bec5f627f987ad4113063c0d3')
options=(!lto)

prepare() {
  cd "${srcdir}/qtwebkit-${_pkgver}"

  patch -Np0 -i "${srcdir}/icu68.patch" # Fix build with ICU 68.x
  patch -Np1 -i "${srcdir}/glib-2.68.patch" # https://github.com/qtwebkit/qtwebkit/issues/1057
  patch -Np1 -i "${srcdir}/qt5-webkit-python-3.9.patch" # Fix build with python 3.9
  patch -Np1 -i "${srcdir}/qt5-webkit-bison-3.7.patch" # Fix build with bison 3.7
  patch -Np1 -i "${srcdir}/qtwebkit-cstdint.patch" # GCC 11.1
  patch -Np1 -i "${srcdir}/qtwebkit-fix-build-gcc14.patch" # GCC 14.1
  patch -Np1 -i "${srcdir}/qt5-webkit-icu75.patch" # ICU 75
  patch -Np1 -i "${srcdir}/webkit-offlineasm-warnings-ruby27.patch" # Ruby 3.2
  patch -Np1 -i "${srcdir}/qt5-webkit-icu76.patch" # Fix build with ICU 76
}

build() {
  cd "${srcdir}"

  cmake -B build -S "qtwebkit-${_pkgver}" \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_CXX_FLAGS="${CXXFLAGS} -DNDEBUG" \
    -DPORT=Qt \
    -DUSE_LD_GOLD=OFF \
    -DENABLE_XSLT=OFF \
    -DENABLE_TOOLS=OFF
  cmake --build build

  # disabling XSLT to build https://github.com/qtwebkit/qtwebkit/issues/1097
}

package() {
  cd "${srcdir}"

  DESTDIR="${pkgdir}" cmake --install build
}
