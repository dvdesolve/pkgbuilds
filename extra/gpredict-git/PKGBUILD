# Maintainer: Viktor Drobot (aka dviktor) linux776 [at] gmail [dot] com
# Contributor: not_anonymous <nmlibertarian@gmail.com>
# Contributor: Andrey Semisokhin <customs.rnd@gmail.com>
# Contributor: Bob Finch <w9ya@qrparci.net>

pkgname=gpredict-git
pkgver=r1647.91a4a3f
pkgrel=1
pkgdesc="Gpredict is a real-time satellite tracking and orbit prediction application."
arch=(i686 x86_64)
url="http://gpredict.oz9aec.net/"
license=(GPL-2.0-only)
depends=(cairo 'curl>=7.19' gdk-pixbuf2 glib2 glibc goocanvas gtk3 hamradio-menus hicolor-icon-theme pango)
makedepends=(autoconf automake git intltool)
optdepends=('hamlib: rig interfacing/control'
            'libreoffice: read /usr/share/doc/gpredict/um/gredict-user-manual.odt')
conflicts=(gpredict)
provides=(gpredict)
options=(!emptydirs)
source=("${pkgname}::git+https://github.com/csete/gpredict.git")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}"

  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${pkgname}"

  ./autogen.sh
  ./configure --prefix=/usr

  make
}

package() {
  cd "${srcdir}/${pkgname}"

  make DESTDIR=${pkgdir} install

  mkdir -p "${pkgdir}/usr/share/doc/gpredict/"{notes,um}
  cp -r doc/notes/* "${pkgdir}/usr/share/doc/gpredict/notes"
  cp -r doc/um/* "${pkgdir}/usr/share/doc/gpredict/um"

  rm -rf "${pkgdir}/usr/share/gpredict/COPYING"
  sed -i '$ a\X-DCOP-ServiceType=none' "${pkgdir}/usr/share/applications/gpredict.desktop"
  sed -i '$ a\X-KDE-SubstituteUID=false' "${pkgdir}/usr/share/applications/gpredict.desktop"
}
