# Maintainer: Viktor Drobot (aka dviktor) linux776 [at] gmail [dot] com
# Contributor: Peter Ivanov <ivanovp@gmail.com>
# Contributor: Dominik Heidler <dheidler@gmail.com>

pkgname=gqrx-git
pkgver=r2648.1a8ab3a3
pkgrel=1
pkgdesc="SDR receiver for Funcube Dongle, RTL-SDR, USRP and OsmoSDR devices"
url="http://gqrx.dk/"
license=('GPL-3.0-only AND Apache-2.0')
arch=(i686 x86_64)
depends=(alsa-lib boost-libs fftw gcc-libs glibc gnuradio gnuradio-osmosdr gsl libpulse libuhd libusb libvolk pulse-native-provider qt5-base qt5-svg)
makedepends=(boost cmake git make patch)
optdepends=('gr-osmosdr-git: support of OsmoSDR devices')
conflicts=(gqrx)
provides=(gqrx)
source=("${pkgname}::git+https://github.com/csete/gqrx.git"
        "21-fcd.rules"
        "gqrx.png"
        "gqrx.desktop")
sha256sums=('SKIP'
            '490fdc3e16c8d6ae38894896f622531e2bf617963165ce2430a3c132d91ba5e5'
            'abb0c2598830c9fe728d8cd0d2f187d10331f7cb607520ff3664afd67a3a279d'
            'c67ff791ff9e1380e207ea9af32a0273f738a6b19d3b11251a94b931d6c00434')

pkgver() {
  cd "${srcdir}/${pkgname}"

  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${pkgname}"

  cmake .
  make
}

package() {
  cd "${srcdir}"

  install -m755 -d "${pkgdir}/etc/udev/rules.d"
  install -m755 -d "${pkgdir}/usr/bin"
  install -m755 -d "${pkgdir}/usr/share/applications"
  install -m755 -d "${pkgdir}/usr/share/pixmaps"

  install -Dm644 21-fcd.rules "${pkgdir}/etc/udev/rules.d/21-fcd.rules"
  install -Dm644 gqrx.desktop "${pkgdir}/usr/share/applications/gqrx.desktop"
  install -Dm644 gqrx.png "${pkgdir}/usr/share/pixmaps/gqrx.png"

  cd "${pkgname}/src"

  install -Dm755 gqrx "${pkgdir}/usr/bin/gqrx"
}
