# Maintainer: Viktor Drobot (aka dviktor) linux776 [at] gmail [dot] com
# Contributor: anish [ at ] gatech [dot] edu

_pkgname=dump1090-fa
pkgname=${_pkgname}-git
pkgver=r2045.1d3a01fc
pkgrel=1
pkgdesc="Dump1090 is a simple Mode S decoder for RTLSDR devices"
arch=(i686 x86_64)
url="https://github.com/flightaware/dump1090"
license=(GPL-2.0-or-later)
depends=(bash bladerf glibc hackrf limesuite ncurses rtl-sdr soapysdr)
makedepends=(git patch)
optdepends=('lighttpd: interactive map display support')
conflicts=(dump1090)
provides=(dump1090)
source=("${pkgname}::git+https://github.com/flightaware/dump1090"
        "dump1090.service"
        "lighttpd.conf"
        "wisdom.patch"
        "nullstrs.patch")
backup=("usr/share/dump1090-fa/html/config.js")
install='dump1090-fa.install'
sha256sums=('SKIP'
            '137a74e573b6fbace7bf39e5ccf77f2ee4f5f5bbb08efb3634a82f8f2cdfd2aa'
            'cd1667653410c6e3a2d8b0069b6e2ef4f92ade686530529c74fe60c26d1b3e4f'
            'c920cba6bf466173c4a52a3ebb5611f301f2123e8ed3b403b7d2f42ac751c303'
            'fcf816d94bdde15ab347c8fc7b4853458e3f61fe01467e14ef81cdf84230e1a8')

pkgver() {
  cd "${srcdir}/${pkgname}"

  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}/${pkgname}"

  patch -Np0 -i "${srcdir}/wisdom.patch"
  patch -Np0 -i "${srcdir}/nullstrs.patch"
}

build() {
  cd "${srcdir}/${pkgname}"

  make all faup1090 EXTRACFLAGS=-DHTMLPATH=\\\"/usr/share/dump1090-fa/html\\\"
}

package() {
  install -dm755 "${pkgdir}/usr/share/dump1090-fa/html"
  install -dm755 "${pkgdir}/usr/share/dump1090-fa/bladerf"

  install -Dm755 "${srcdir}/${pkgname}/dump1090" "${pkgdir}/usr/bin/dump1090"
  install -Dm755 "${srcdir}/${pkgname}/view1090" "${pkgdir}/usr/bin/view1090"
  install -Dm755 "${srcdir}/${pkgname}/faup1090" "${pkgdir}/usr/lib/piaware/helpers/faup1090"
  install -Dm755 "${srcdir}/${pkgname}/starch-benchmark" "${pkgdir}/usr/lib/dump1090-fa/starch-benchmark"
  install -Dm755 "${srcdir}/${pkgname}/debian/generate-wisdom" "${pkgdir}/usr/lib/dump1090-fa/generate-wisdom"
  install -Dm644 "${srcdir}/dump1090.service" "${pkgdir}/usr/lib/systemd/system/dump1090.service"
  install -Dm644 "${srcdir}"/lighttpd.conf "${pkgdir}/usr/share/dump1090-fa/lighttpd.conf"

  cp -r "${srcdir}"/"${pkgname}"/public_html/* "${pkgdir}/usr/share/dump1090-fa/html"
  cp -r "${srcdir}"/"${pkgname}"/bladerf/* "${pkgdir}/usr/share/dump1090-fa/bladerf"
}
