# Maintainer: Viktor Drobot (aka dviktor) linux776 [at] gmail [dot] com
# Contributor: Alexandre Rouma <whatsthetgeek@gmail.com>
# Contributor: Ã©clairevoyant
# Contributor: Marin Atanasov Nikolov <dnaeon@gmail.com>

_pkgname=sdrpp
pkgname=${_pkgname}-git
pkgver=r1742.dd9b8db6
pkgrel=1
pkgdesc="The bloat-free SDR receiver"
arch=(x86_64)
url="https://www.sdrpp.org/"
license=(GPL-3.0-only)
depends=(fftw gcc-libs glibc glew glfw libglvnd libvolk zstd)
_plugindeps=(airspy airspyhf-git bladerf codec2 hackrf libad9361 libiio libsdrplay limesuite rtaudio rtl-sdr portaudio soapysdr)
makedepends=(cmake git "${_plugindeps[@]}")
optdepends=("${_plugindeps[@]}")
conflicts=(sdrpp)
provides=(sdrpp)
source=("${_pkgname}::git+https://github.com/AlexandreRouma/SDRPlusPlus.git")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_pkgname}"

  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}"

  cmake -B build -S ${_pkgname} \
    -DOPT_BUILD_BLADERF_SOURCE=ON \
    -DOPT_BUILD_LIMESDR_SOURCE=ON \
    -DOPT_BUILD_SDRPLAY_SOURCE=ON \
    -DOPT_BUILD_NEW_PORTAUDIO_SINK=ON \
    -DOPT_BUILD_M17_DECODER=ON \
    -DCMAKE_POLICY_VERSION_MINIMUM=3.5
  make -C build
}

package() {
  cd "${srcdir}"

  make -C build DESTDIR="${pkgdir}" install
}
