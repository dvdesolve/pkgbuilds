# Maintainer: Viktor Drobot (aka dviktor) linux776 [at] gmail [dot] com
# Contributor: Dan McCurry <dan.mc at protonmail dot com>
# Contributor: Ethan Best <ethan at totalsecond dot com>
# Contributor: deadlte

pkgname=libsdrplay
pkgver=3.14.0
pkgrel=1
pkgdesc="Modules for the SDRplay receiver"
url="http://www.sdrplay.com"
license=(custom)
arch=(i686 x86_64)
depends=('libusb>=1.0' systemd-libs)
source=("http://www.sdrplay.com/software/SDRplay_RSP_API-Linux-${pkgver}.run"
        "sdrplay.service"
        "66-sdrplay.rules")
sha256sums=('0a0f3e2009298d1d1163158f4e22fc4fc9d41cd4f8647191474ef25beaf0fc4d'
            '5514e3618699e0f6bfad85dc1381bb18ee938269c30ac81f25c8888f0cddd565'
            'a1b640b37f95ce7404363dc65b9e9608bb221aece353a370c52a0f8fa6b4bdab')

prepare() {
  cd "${srcdir}"

  # unpack installation blob
  sh SDRplay_RSP_API-Linux-${pkgver}.run --tar xf
}

package() {
  cd "${srcdir}"

  # get API version
  _apivers=$(sed -n 's/^VERS="\(.*\)"/\1/p' install_lib.sh)

  install -D -m644 sdrplay_license.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -D -m644 "${CARCH}/libsdrplay_api.so.${_apivers}" "${pkgdir}/usr/lib/libsdrplay_api.so.${_apivers}"
  install -D -m755 "${CARCH}/sdrplay_apiService" "${pkgdir}/usr/bin/sdrplay_apiService"
  install -D -m644 "sdrplay.service" "${pkgdir}/etc/systemd/system/sdrplay.service"

  (cd inc && find . -type f -exec install -D -m644 "{}" "${pkgdir}/usr/include/{}" \;)

  install -D -m644 66-sdrplay.rules "${pkgdir}/etc/udev/rules.d/66-sdrplay.rules"

  cd "${pkgdir}/usr/lib"
  ln -s libsdrplay_api.so.${_apivers} libsdrplay_api.so.2
  ln -s libsdrplay_api.so.${_apivers} libsdrplay_api.so
}
