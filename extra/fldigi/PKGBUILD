# Maintainer: Viktor Drobot (aka dviktor) linux776 [at] gmail [dot] com
# Contributor: not_anonymous <nmlibertarian@gmail.com>
# Contributor: Andreas Schreiner <andreas.schreiner@sonnenmulde.at>
# Contributor: [Vitaliy Berdinskikh](mailto:ur6lad@archlinux.org.ua) aka UR6LAD
# Contributor: Bob Finch <w9ya@qrparci.net>

pkgname=fldigi
pkgver=4.2.06
pkgrel=1
pkgdesc="Digital Modem Program for Amateur Radio"
arch=(i686 x86_64)
url="http://w1hkj.com"
license=(GPL-2.0-or-later)
depends=(cty fltk 'flxmlrpc>=1.0.1' gcc-libs glibc hamlib hamradio-menus libpng libpulse libsamplerate libsndfile libx11 portaudio systemd-libs)
optdepends=('pulseaudio: pulseaudio support')
source=(http://w1hkj.com/files/fldigi/${pkgname}-${pkgver}.tar.gz)
sha256sums=('4360de225d6f8cfeb9bb6a5be6ac492e52702c8f704f87609c452d3bcb1b6c08')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr --enable-tls --with-flxmlrpc --without-asciidoc
  make ASCIIDOC_ICONS_DIR=/etc/asciidoc/images/icons/
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
}
