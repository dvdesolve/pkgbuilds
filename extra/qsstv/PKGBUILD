# Maintainer: Viktor Drobot (aka dviktor) linux776 [at] gmail [dot] com
# Contributor: Alexander Fasching <fasching.a91@gmail.com>
# Contributor: Andreas Schreiner <andreas.schreiner@sonnenmulde.at>
# Contributor: Bob Finch <w9ya@qrparci.net>

pkgname=qsstv
pkgver=9.5.8
pkgrel=3
pkgdesc="Receive and transmit images over radio using analog SSTV or digital DRM"
url="https://github.com/ON4QZ/QSSTV"
license=(GPL3)
arch=(i686 x86_64)
depends=(qt5-base libpulse v4l-utils hamlib fftw openjpeg2 alsa-lib)
source=(https://www.qsl.net/o/on4qz//qsstv/downloads/${pkgname}_${pkgver}.tar.gz
        ${pkgname}.desktop)
sha256sums=('c03f7fa5c680ced8fd331c25ff3e47440c9aedb48ec7b66255c6aa0ed88e7a68'
            'dd464d07499b9002ddcd71a94a6e2c2a68264d80b39ab8bf89bfd1a475108286')

build() {
  cd "${srcdir}/${pkgname}"

  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}"

  make INSTALL_ROOT="${pkgdir}" install

  install -Dm644 "${srcdir}/qsstv.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -Dm644 "${srcdir}/${pkgname}/icons/${pkgname}.png" "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
}
